/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction.
 * 
 * Author: Naga Kandasamy
 * Date created: October 6, 2020
 * Date modified: July 21, 2023
 * 
 * Student name(s): Noah Robinson   
 * Date modified: 7/30/23
 * 
 * Notes: This project is part of www.nand2tetris.org and the book "The Elements of Computing Systems" by Nisan and Schocken, MIT Press.
 */

CHIP CPU {
    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Implement the D register, which is loaded under the following conditions:
    // if (instruction[15] == 1) AND instruction[4] == 1
    // instruction[15]: bit specifying A-type or C-type instruction
    // instruction[4]: dest bit d2 which is the load signal for the D register
    Not(in=instruction[15], out=aType);
    Not(in=aType, out=cType);
    And(a=cType, b=instruction[4], out=loadd);
    DRegister(in=ALUtemp, load=loadd, out=dOut);    
    // Implement the A register, which is loaded under the following conditions:
    // if (instruction[15] == 0) OR (instruction[15] == 1 AND instruction[5] == 1) 
    // instruction[15]: 0 for A-type and 1 for C-type instruction 
    // instruction[5]: dest bit d1 which is the load signal for the A register
    And(a=cType, b=instruction[5], out=ALUtoA);    
    Mux16(a=instruction, b=ALUtemp, sel=ALUtoA, out=Aregin);
    Or(a=aType, b=ALUtoA, out=loadA);  
    ARegister(in=Aregin, load=loadA, out=aOutput);
    Mux16(a=aOutput, b=inM, sel=instruction[12], out=muxOut);  
   
    // Implement the ALU. The control signals are c1, c2, c3, c4, c5, c6, which correspond to instruction[11..6]
    ALU(x=dOut, y=muxOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUtemp, zr=zero, ng=neg);

    // Check for JEQ AND == 0
    And(a=zero, b=instruction[1], out=jeq);    
    // Check for JLT AND < 0
    And(a=neg, b=instruction[2], out=jlt);   
    Or(a=zero, b=neg, out=zrng);
    Not(in=zrng, out=pos);         
    // Check for JGT AND > 0 indication from ALU
    And(a=pos, b=instruction[0], out=jgt); 
    // Check for JLE AND <= 0
    Or(a=jeq, b=jlt, out=jle);
    // Check for JNE AND != 0   
    // Check for JMP
    Or(a=jle, b=jgt, out=jump);
    // Generate the load signal for PC, if the instruction is C-type and any of the above conditions are true
    And(a=cType, b=jump, out=PCload); 
    // Generate the inc signal for PC
    Not(in=PCload, out=PCinc);                 
    PC(in=aOutput, inc=PCinc, load=PCload, reset=reset, out[0..14]=pc);
    
    // Set the writeM control line if instruction[15] == 1 AND instruction[3] == 1
    // instruction[15]: 0 if A-type, 1 if C-type instruction 
    // instruction[3]: the d3 bit, specifying the load signal to memory
    And(a=cType, b=instruction[3], out=writeM);
    Or16(a=false, b=aOutput, out[0..14]=addressM);
    Or16(a=false, b=ALUtemp, out=outM);
}